plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.2.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

shadowJar {
    archiveClassifier.set(null)
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "Minecraft Libraries"
        url = "https://libraries.minecraft.net"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    api "io.papermc.paper:paper-api:${paperVersion}-R0.1-SNAPSHOT"
    api "org.jetbrains.kotlin:kotlin-stdlib"
}

kotlin {
    jvmToolchain(21)
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            def tagVersion = System.getenv('VERSION')

            groupId = "de.raphaelgoetz"
            artifactId = "astralis"
            version = tagVersion ?: '0.0.1-SNAPSHOT'

            artifact shadowJar

            pom {
                name = "astralis"
                url = "https://github.com/raphael-goetz/astralis"
                description = "Astralis is a Minecraft-Framework designed to create awesome build-tools"

                licenses {
                    license {
                        name = "Apache 2.0"
                        url = "https://github.com/raphael-goetzastralis/blob/master/LICENSE"
                    }
                }

                developers {
                    developer {
                        id = "raphael-goetz"
                        name = "Raphael Goetz"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/raphael-goetz/astralis.git"
                    developerConnection = "scm:git:git@github.com:raphael-goetz/raphael-goetz.git"
                    url = "https://github.com/raphael-goetz/astralis"
                    tag = "HEAD"
                }

                ciManagement {
                    system = "GitHub Actions"
                    url = "https://github.com/raphael-goetz/astralis/actions"
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/raphael-goetz/astralis'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}